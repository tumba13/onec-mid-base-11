
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	
	// Добавляет на форму группу СкидкаКоманда В ГруппаШапкаЛево,реквизит "ДК_СогласованнаяСкидка",кнопку "ДК_ПересчитатьТаблицу" - Минкова 24.07.2024
	СозданиеЭлементовФормы();
	// - Минкова 24.07.202	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// Начало доработки - Минкова 01.08.2024
    // Исходный код:
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	КоэффициентСкидки = 1 - (ТекущиеДанные.Скидка + Объект.ДК_СогласованнаяСкидка) / 100;
	Если КоэффициентСкидки < 0 Тогда
		Сообщить("Скидка больше 100%. Измените размер скидки!", СтатусСообщения.ОченьВажное);
		ТекущиеДанные.Сумма = 0;
		Возврат;
	Иначе
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	КонецЕсли;
	
	РассчитатьСуммуДокумента();
	// Конец доработки	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура СозданиеЭлементовФормы() Экспорт
	
	// Начало доработки - Минкова 24.07.2024 
	// Добавялет группу СкидкаКоманда В ГруппаШапкаЛево
	Группа = Элементы.Добавить("СкидкаКоманда", Тип("ГруппаФормы"), ЭтаФорма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = ЛОЖЬ;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	// Добавялет реквизит "ДК_СогласованнаяСкидка" на форму
	ПолеВвода = Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ДК_СогласованнаяСкидкаПриИзменении");
	// Добавялет кнопку "ДК_ПересчитатьТаблицу" на форму 
	Команда = Команды.Добавить("ДК_ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "ДК_ПересчитатьТаблицуПосле";
	
    КнопкаФормы = Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Группа);
	КнопкаФормы.ИмяКоманды = "ДК_ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	// Конец доработки		
	
	
КонецПроцедуры 


&НаКлиенте
Процедура ДК_СогласованнаяСкидкаПриИзменении(Элемент)
	
	// {{ Всплывающее окно с вопросом о пересчете ТЧ - Минкова 24.07.2024
	ЗадатьВопросОПересчетеСкидкиАсинхронно();
	// }}
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросОПересчетеСкидкиАсинхронно()
	
	// {{ Проверка на наличие строк в ТЧ - Минкова 24.07.2024
	Если Объект.Товары.Количество() < 1 И Объект.Услуги.Количество() < 1 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "В табличных частях нет строк";	
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	// Всплывающее окно с вопросом о пересчете ТЧ (да/нет)
	ТекстВопроса = "Изменен процент скидки. Пересчитать таблицу?";
	Режим = РежимДиалогаВопрос.ДаНет;
	ОтветПользователя = Ждать ВопросАсинх(ТекстВопроса, Режим, 0);
	
	Если ОтветПользователя = КодВозвратаДиалога.Нет Тогда
		Возврат;
	ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Да Тогда
		ПересчитатьСтрокиТЧ();
		РассчитатьСуммуДокумента();
	КонецЕсли;  
	// }}

	
КонецПроцедуры

&НаКлиенте
Процедура ДК_ПересчитатьТаблицуПосле(Команда)
	
	// {{Команда ПересчитатьТаблицу - Минкова 24.07.2024
	ПересчитатьСтрокиТЧ();
	РассчитатьСуммуДокумента();
	// }} 

КонецПроцедуры 


&НаКлиенте
Процедура ПересчитатьСтрокиТЧ()
	
	// {{ Пересчитывает таблицу с учетом скидки - Минкова 01.08.2024
	ТекстСообщения = "Скидка больше 100%. Измените размер скидки!";
	
	Для Каждого СтрокаТаблинчойЧасти Из Объект.Товары Цикл
		КоэффициентСкидки = 1 - (СтрокаТаблинчойЧасти.Скидка + Объект.ДК_СогласованнаяСкидка) / 100;
			Если КоэффициентСкидки < 0 Тогда			
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = ТекстСообщения;	
				Сообщение.Сообщить();
				СтрокаТаблинчойЧасти.Сумма = 0;
				Возврат;
			Иначе
				СтрокаТаблинчойЧасти.Сумма = (СтрокаТаблинчойЧасти.Цена * СтрокаТаблинчойЧасти.Количество) * КоэффициентСкидки;
			КонецЕсли;
	КонецЦикла;	
	 
	Для Каждого СтрокаТаблинчойЧасти Из Объект.Услуги Цикл
		КоэффициентСкидки = 1 - (СтрокаТаблинчойЧасти.Скидка + Объект.ДК_СогласованнаяСкидка) / 100;
			Если КоэффициентСкидки < 0 Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = ТекстСообщения;	
				Сообщение.Сообщить();
				СтрокаТаблинчойЧасти.Сумма = 0;
				Возврат;
			Иначе
				СтрокаТаблинчойЧасти.Сумма = (СтрокаТаблинчойЧасти.Цена * СтрокаТаблинчойЧасти.Количество) * КоэффициентСкидки;
			КонецЕсли;		
	КонецЦикла;
	// }}
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти
